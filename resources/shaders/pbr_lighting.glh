const uint MAX_CASCADES = 4; // TODO: should be set from ShadowAtlas::MAX_CASCADES

#include "shared-structs.glh"
#include "rect_light_ltc.glh"
#include "shapes.glh"

#ifndef PI
    #define PI 3.141592653589793238462643
#endif

const float MAX_REFLECTION_LOD = 4.0; // mips in range [0, 4]

layout (location = 0) in vec2 in_texcoord;
layout (location = 1) in vec3 in_world_pos;
layout (location = 2) in vec3 in_view_pos;
layout (location = 3) in vec4 in_clip_pos;
layout (location = 4) in vec3 in_normal;

layout (location = 6)  in vec3 in_csm_light_view_pos[MAX_CASCADES];
layout (location = 10) in vec3 in_csm_light_uv_pos[MAX_CASCADES];

layout(binding = 0) uniform sampler2D u_albedo_map;
layout(binding = 1) uniform sampler2D u_normal_map;
layout(binding = 2) uniform sampler2D u_metallic_map;
layout(binding = 3) uniform sampler2D u_roughness_map;
layout(binding = 4) uniform sampler2D u_ao_map;
layout(binding = 5) uniform sampler2D u_emissive_map;

layout (binding = 6) uniform samplerCube u_irradiance_map;
layout (binding = 7) uniform samplerCube u_prefiltered_map;
layout (binding = 8) uniform sampler2D   u_brdf_lut;
// 9, 10 is in area_light_ltc.glh

uniform bool u_has_albedo_map;
uniform bool u_has_normal_map;
uniform bool u_has_metallic_map;
uniform bool u_has_roughness_map;
uniform bool u_has_ao_map;
uniform bool u_has_emissive_map;

uniform vec3  u_albedo;
uniform float u_metallic;
uniform float u_roughness;
uniform float u_ao;
uniform vec3  u_emission;
uniform float u_emission_strength;

uniform vec3  u_cam_pos;
uniform float u_ibl_strength;
uniform float u_specular_max_distance;

struct MaterialProperties
{
    vec3 albedo;
    vec3 normal;
    vec3 emission;
    float metallic;
    float roughness;
    float ao;
    float opacity;
};

// returns [1, 0] whther the fragment corresponding to 'atlas_uv' has LOS to the light
float fadeByDistance(float distance, float hard_limit)
{
	return 1 - smoothstep(hard_limit*0.8, hard_limit, distance);
}

vec3 getNormalFromMap()
{
    vec3 tangent_normal = texture(u_normal_map, in_texcoord).xyz * 2.0 - 1.0;

    vec3 Q1  = dFdx(in_world_pos);
    vec3 Q2  = dFdy(in_world_pos);
    vec2 st1 = dFdx(in_texcoord);
    vec2 st2 = dFdy(in_texcoord);

    vec3 N   = normalize(in_normal);
    vec3 T   = normalize(Q1*st2.t - Q2*st1.t);
    vec3 B   = -normalize(cross(N, T));
    mat3 TBN = mat3(T, B, N);

    return normalize(TBN * tangent_normal);
}

MaterialProperties getMaterialProperties(vec3 normal)
{
    MaterialProperties material;

    vec4 albedo_opacity = texture(u_albedo_map, in_texcoord);
    material.albedo    = u_has_albedo_map    ? albedo_opacity.rgb                       : u_albedo;
    material.emission  = (u_has_emissive_map  ? texture(u_emissive_map, in_texcoord).rgb : u_emission) * u_emission_strength;
    material.normal    = u_has_normal_map    ? getNormalFromMap()                       : normal;
    material.metallic  = u_has_metallic_map  ? texture(u_metallic_map, in_texcoord).b   : u_metallic;
    material.roughness = u_has_roughness_map ? texture(u_roughness_map, in_texcoord).g  : u_roughness;
    material.ao        = u_has_ao_map        ? texture(u_ao_map, in_texcoord).r         : u_ao;
    material.opacity   = albedo_opacity.a;

    return material;
}

float linearDepth(float depth, float z_near, float z_far)
{
    float ndc = depth * 2.0 - 1.0;
    float linear_depth = 2.0 * z_near * z_far / (z_far + z_near - ndc * (z_far - z_near));

    return linear_depth;
}

vec3 fresnelSchlick(float cos_theta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(max(1.0 - cos_theta, 0.0), 5.0);
}

vec3 fresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness)
{
    return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(max(1.0 - cosTheta, 0.0), 5.0);
}

float distributionGGX(vec3 n, vec3 h, float roughness)
{
    float a = roughness * roughness;
    float a2 = a * a;
    float NdotH = max(dot(n, h), 0.0);
    float NdotH2 = NdotH * NdotH;

    float num = a2;
    float denominator = (NdotH2 * (a2 - 1.0) + 1.0);
    denominator = PI * denominator * denominator;

    return num / max(denominator, 1e-5);
}

float geometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r * r) / 8.0;

    float num   = NdotV;
    float denominator = NdotV * (1.0 - k) + k;

    return num / max(denominator, 1e-5);
}

float geometrySmith(vec3 n, vec3 wo, vec3 wi, float roughness)
{
    float NdotV = max(dot(n, wo), 0.0);
    float NdotL = max(dot(n, wi), 0.0);

    float ggx1 = geometrySchlickGGX(NdotL, roughness);
    float ggx2 = geometrySchlickGGX(NdotV, roughness);

    return ggx1 * ggx2;
}

vec3 indirectLightingIBL(vec3 world_pos, MaterialProperties material)
{
    vec3 wo = normalize(u_cam_pos - world_pos);
    vec3 r  = reflect(-wo, material.normal);

    // fresnel reflectance
    vec3 F0 = vec3(0.04);
         F0 = mix(F0, material.albedo, material.metallic);

    vec3 F  = fresnelSchlickRoughness(max(dot(material.normal, wo), 0.0), F0, material.roughness);
    vec3 ks = F;
    vec3 kd = 1.0 - ks;
         kd = kd * (1.0 - material.metallic);

    // diffuse IBL term
    vec3 irradiance = texture(u_irradiance_map, material.normal).rgb;
    vec3 diffuse    = material.albedo * irradiance;

    // specular IBL term
    vec3 prefiletered_color = textureLod(u_prefiltered_map, r, material.roughness * MAX_REFLECTION_LOD).rgb;
    vec2 brdf               = texture(u_brdf_lut, vec2(max(dot(material.normal, wo), 0.0), material.roughness)).rg;
    vec3 specular           =  prefiletered_color * (F * brdf.x + brdf.y);

    // total indirect lighting
    return (kd * diffuse + specular) * material.ao * u_ibl_strength;
}

vec3 pbr(GPULight light, vec3 direction, vec3 world_pos, float attenuation, MaterialProperties material)
{
	vec3 wo         = normalize(u_cam_pos - world_pos);
	vec3 radiance   = vec3(0);

	// fresnel reflectance at normal incidence
	vec3 F0 = vec3(0.04); // base reflectance
		 F0 = mix(F0, material.albedo, material.metallic);

    // calculate radiance
    {
		vec3 wi               = normalize(direction);
		float NdotL           = max(dot(material.normal, wi), 0.0);
		vec3 h                = normalize(wo + wi);
		vec3 light_irradiance = light.color * light.intensity;

		// cook-torrance brdf
		vec3  F   = fresnelSchlick(max(dot(h, wo), 0.0), F0);

		vec3 specular = vec3(0);
        // fade specular by distance from light
		float light_distance = distance(world_pos, light.position);
		if(light_distance < u_specular_max_distance)
		{
			// also fade by light's distance from camera
			float edge_distance = max(0, distance(u_cam_pos, light.position) - light.affect_radius);
			if(edge_distance < u_specular_max_distance*2)
			{
				float cam_fade = fadeByDistance(edge_distance, u_specular_max_distance*2);
				float fade = 1 - clamp((light_distance - u_specular_max_distance*0.8)/(u_specular_max_distance - u_specular_max_distance*0.8), 0, 1);
				float NDF = distributionGGX(material.normal, h, material.roughness);
				float G   = geometrySmith(material.normal, wo, wi, material.roughness);

				vec3  num         = NDF * G * F;
				float denominator = 4.0 * max(dot(material.normal, wo), 0.0) * NdotL;
				specular = cam_fade * fade * num / max(denominator, 1e-5);
			}
		}

		vec3 ks = F;
		vec3 kd = 1.0 - ks;
			 kd = kd * (1.0 - material.metallic);

		radiance += (kd * material.albedo / PI + specular) * light_irradiance * NdotL * attenuation;
	}

	return radiance;
}

vec3 pbr_dir(GPULight light, vec3 direction, vec3 world_pos, float attenuation, MaterialProperties material)
{
	vec3 wo         = normalize(u_cam_pos - world_pos);
	vec3 radiance   = vec3(0);

	// fresnel reflectance at normal incidence
	vec3 F0 = vec3(0.04); // base reflectance
		 F0 = mix(F0, material.albedo, material.metallic);

    // calculate radiance
    {
		vec3 wi               = normalize(direction);
		float NdotL           = max(dot(material.normal, wi), 0.0);
		vec3 h                = normalize(wo + wi);
		vec3 light_irradiance = light.color * light.intensity;

		// cook-torrance brdf
		vec3  F   = fresnelSchlick(max(dot(h, wo), 0.0), F0);

		float NDF = distributionGGX(material.normal, h, material.roughness);
		float G   = geometrySmith(material.normal, wo, wi, material.roughness);

		vec3  num         = NDF * G * F;
		float denominator = 4.0 * max(dot(material.normal, wo), 0.0) * NdotL;
		vec3 specular = num / max(denominator, 1e-5);

		vec3 ks = F;
		vec3 kd = 1.0 - ks;
			 kd = kd * (1.0 - material.metallic);

		radiance += (kd * material.albedo / PI + specular) * light_irradiance * NdotL * attenuation;
	}

	return radiance;
}


vec3 calcAmbience(vec3 radiance, MaterialProperties material)
{
	return radiance * material.albedo;
}

vec3 calcPointLight(GPULight light, vec3 world_pos, MaterialProperties material)
{
    vec3  light_direction = light.position - world_pos;
    float distance_sq = dot(light_direction, light_direction);
	float attenuation = lightFalloff(distance_sq, light.affect_radius*light.affect_radius);

    return pbr(light, light_direction, world_pos, attenuation, material);
}

vec3 calcDirectionalLight(GPULight light, vec3 world_pos, MaterialProperties material)
{
    return pbr_dir(light, -light.direction, world_pos, 1, material);
}

vec3 calcSpotLight(GPULight light, vec3 world_pos, MaterialProperties material)
{
    vec3  L            = normalize(world_pos - light.position);
    vec3  pos_to_light = light.position - world_pos;

	float distance_sq = dot(pos_to_light, pos_to_light);

	float attenuation     = lightFalloff(distance_sq, light.affect_radius*light.affect_radius);
          attenuation    *= spotRadialFalloff(L, -light.direction, light.inner_angle, light.outer_angle);
    vec3  light_direction = pos_to_light;

    return pbr(light, light_direction, world_pos, attenuation, material);
}

vec3 calcRectLight(GPULight light, vec3 world_pos, MaterialProperties material)
{
	// NOTE: this is not full PBR

	// https://blog.selfshadow.com/publications/s2017-shading-course/

	// TODO: degenerate to simpler (e.g. nearest-point) at larger distance, or even point light (only double-sided!)

	light.intensity = pow(light.intensity, RECT_INTENSITY_POWER);

    vec3 F0         = vec3(0.04); // base reflectance
    vec3 diff_color = material.albedo * (1.0 - material.metallic);
    vec3 spec_color = mix(F0, material.albedo, material.metallic);

    vec3  N         = material.normal;
    vec3  V         = normalize(u_cam_pos - world_pos);
    float cos_theta = max(dot(N, V), 0.0);

	bool two_sided = IS_DOUBLE_SIDED(light);

    // diffuse term
    vec4 shape_data[4];
    shape_data[0] = vec4(light.position + light.shape_data[0].xyz, 1);
    shape_data[1] = vec4(light.position + light.shape_data[1].xyz, 1);
    shape_data[2] = vec4(light.position + light.shape_data[2].xyz, 1);
    shape_data[3] = vec4(light.position + light.shape_data[3].xyz, 1);
    vec3 diff  = ltcEvaluate(N, V, world_pos, mat3(1), shape_data, two_sided);
         diff *= diff_color;

    // specular term
    vec2 coords  = ltcCoords(cos_theta, material.roughness);
    mat3 Minv    = ltcMatrix(s_ltc_mat, coords);
    vec2 schlick = texture2D(s_ltc_amp, coords).xy;

    vec3 spec  = ltcEvaluate(N, V, world_pos, Minv, shape_data, two_sided);
         spec *= spec_color * schlick.x + (1.0 - spec_color) * schlick.y;

    vec3 radiance  = light.intensity * (diff + spec);
        radiance /= 2.0 * PI;

    // limit LTC range using closest-point distance to the rectangle and the
    // shared lightFalloff() function (distance_sq, radius_sq).
    vec3 corner0 = shape_data[0].xyz;
    vec3 edgeU = shape_data[1].xyz - corner0;
    vec3 edgeV = shape_data[3].xyz - corner0;
    vec3 center = corner0 + 0.5 * (edgeU + edgeV);
    float halfU = length(edgeU) * 0.5;
    float halfV = length(edgeV) * 0.5;
    vec3 dirU = (halfU > 1e-6) ? normalize(edgeU) : vec3(1,0,0);
    vec3 dirV = (halfV > 1e-6) ? normalize(edgeV) : vec3(0,1,0);

    vec3 local = world_pos - center;
    float u = dot(local, dirU);
    float v = dot(local, dirV);
    float cu = clamp(u, -halfU, halfU);
    float cv = clamp(v, -halfV, halfV);
    vec3 closest = center + dirU * cu + dirV * cv;

    // apply range limited falloff
    float dist_sq = dot(world_pos - closest, world_pos - closest);
    // float att = lightFalloff(dist_sq, light.affect_radius * light.affect_radius);
    float att = 1 - min(dist_sq / (light.affect_radius*light.affect_radius), 1);
    return radiance * att * light.color;
}

vec3 closest_point(Segment segment, vec3 p)
{
	vec3 ba = p - segment.a;
	vec3 bc = segment.b - segment.a;
	float d = dot(ba, bc);
	float len = length(bc);
	float param = 0;
	if (len != 0)
		param = clamp(d / (len * len), 0, 1);
	return segment.a + bc * param;
}

vec3 closest_point(Segment segment, Ray ray)
{
    const float EPSILON = 1e-5;

	// returns the point on segment [segment.a,segment.b] closest to ray (ray.start + t*d, t>=0)
    vec3 v = segment.b - segment.a;
    vec3 w0 = ray.start - segment.a;

    float A = dot(ray.direction, ray.direction);
    float B = dot(ray.direction, v);
    float C = dot(v, v);
    float D = dot(ray.direction, w0);
    float E = dot(v, w0);

    // degenerate segment -> return segment.a
    if(C < EPSILON)
    	return segment.a;

    float denominator = A*C - B*B;
    float t, u;

    if(denominator > EPSILON)
    {
        t = (B*E - C*D) / denominator;
        u = (A*E - B*D) / denominator;
    }
    else
    {
        // parallel: choose a stable default (project segment start onto ray)
        // this will be fixed by clamping below
        t = 0;
        u = E / C;
    }

    // clamp t and u with recomputation if needed
    if(t < 0)
    {
        t = 0;
        u = E / C;
        u = clamp(u, 0, 1);
    }
    else
    {
        if(u < 0)
        {
            u = 0;
            if (A > EPSILON)
           		t = max(0, dot(ray.direction, segment.a - ray.start) / A);
            else
            	t = 0;
        }
        else if (u > 1)
        {
            u = 1;
            if (A > EPSILON)
            	t = max(0, dot(ray.direction, segment.b - ray.start) / A);
            else
           		t = 0;
        }
    }

    // final closest point on segment
	return segment.a + v * u;
}

vec3 calcTubeLight(GPULight light, vec3 world_pos, MaterialProperties material)
{
	// simulate using a fragment-facing rect light, thick as tube diameter

	// https://blog.selfshadow.com/publications/s2017-shading-course/
	// https://wickedengine.net/2017/09/area-lights/

	// TODO: degenerate to simpler (e.g. nearest-point) at larger distance, or even point light

	Segment segment = Segment(light.position + light.shape_data[0].xyz, light.position + light.shape_data[1].xyz);
	float thickness = light.shape_data[2].x * 0.5;

	vec3 binormal = normalize(cross(segment.a - segment.b, world_pos - light.position));
	vec3 corner_offset = binormal * thickness;

	GPULight faux_rect = light;
	faux_rect.shape_data[0] = vec4(light.shape_data[1].xyz + corner_offset, 1);
	faux_rect.shape_data[1] = vec4(light.shape_data[0].xyz + corner_offset, 1);
	faux_rect.shape_data[2] = vec4(light.shape_data[1].xyz - corner_offset, 1);
	faux_rect.shape_data[3] = vec4(light.shape_data[0].xyz - corner_offset, 1);

	return calcRectLight(faux_rect, world_pos, material);
}

vec3 calcSphereLight(GPULight light, vec3 world_pos, MaterialProperties material)
{
	// TODO

	//  https://blog.selfshadow.com/publications/s2017-shading-course/

	// TODO: degenerate to point light at larger distance,

	return calcPointLight(light, world_pos, material);
}

vec3 calcDiscLight(GPULight light, vec3 world_pos, MaterialProperties material)
{
	// TODO: simulate as area light of same size
	// see: https://blog.selfshadow.com/publications/s2017-shading-course/

	// TODO: degenerate to point light at larger distance )pm;t dpin;e-soded!)
	// if(edge_distance > half_max_distance)
	//    return calcPointLight(light, world_pos, material);

	vec3 orthogonal = vec3(0, 1, 0);
	if(orthogonal == light.direction)
		orthogonal = vec3(0, 0, 1);

	vec3 right = cross(light.direction, orthogonal);
	vec3 up    = cross(right, light.direction);

	float radius = light.shape_data[0].x;
	vec3 offset_h = right * radius;
	vec3 offset_v = up    * radius;

	GPULight faux_rect = light;
	faux_rect.shape_data[0] = vec4(+ offset_h - offset_v, 1);
	faux_rect.shape_data[1] = vec4(- offset_h - offset_v, 1);
	faux_rect.shape_data[2] = vec4(+ offset_h + offset_v, 1);
	faux_rect.shape_data[3] = vec4(- offset_h + offset_v, 1);

	return calcRectLight(faux_rect, world_pos, material);
}
